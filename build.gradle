plugins {
    id 'org.springframework.boot' version '2.7.3' // https://docs.spring.io/spring-boot/docs/current/gradle-plugin/reference/htmlsingle/
    id 'io.spring.dependency-management' version '1.0.13.RELEASE' // https://docs.spring.io/dependency-management-plugin/docs/current-SNAPSHOT/reference/html/
    id 'application' // https://docs.gradle.org/current/userguide/application_plugin.html
    id 'java' // https://docs.gradle.org/current/userguide/java_plugin.html
    id 'idea' // https://docs.gradle.org/current/userguide/idea_plugin.html
}

group 'com.albertiordache'
version '0.0.1-SNAPSHOT'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly // Spring Boot Devtools
    }
}

final lombokVersion = '1.18.24'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}
